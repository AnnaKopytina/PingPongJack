class Ball {
    field int xCord, yCord, radius;
    field int xSpeed, ySpeed;
    field Player playerLeft;
    field Player playerRight;

    constructor Ball new(Player player1, Player player2, int speedX) {
        let xCord = 512 / 2;
        let yCord = 256 / 2;
        let radius = 6; 
        let xSpeed = speedX;  
        let ySpeed = 2;
        let playerLeft = player1;
        let playerRight = player2;

        return this;
    }

    method void goToCenter() {
        let xCord = 512 / 2;
        let yCord = 256 / 2;
        return;
    }

    method void updateScoreDisplay() {
        do Output.moveCursor(1, 40);
        do Output.printInt(playerRight.getScore());
        
        do Output.moveCursor(1, 22);
        do Output.printInt(playerLeft.getScore());
        return;
    }

    method void moving() {
        // Стираем только мяч, не затрагивая всю область счета
        do Screen.setColor(false);
        do Screen.drawCircle(xCord, yCord, radius);

        let xCord = xCord + xSpeed;
        let yCord = yCord + ySpeed;

        // Проверка столкновения с границами экрана
        if (xCord - radius < 3) {
            let xSpeed = -xSpeed;
            let xCord = 3 + radius; 
        }
        if (xCord + radius > 508) {
            let xSpeed = -xSpeed;
            let xCord = 508 - radius; 
        }
        if (yCord - radius < 25) {
            let ySpeed = -ySpeed;
            let yCord = 25 + radius; 
        }
        if (yCord + radius > 250) {
            let ySpeed = -ySpeed;
            let yCord = 250 - radius; 
        }

        // Отбил левый игрок
        if (xSpeed < 0) {
            if (((xCord - radius) < ((playerLeft.getX() + playerLeft.getSizeX()) + 1)) &
                (yCord > playerLeft.getY()) & 
                (yCord < (playerLeft.getY() + playerLeft.getSizeY()))) {

                let xSpeed = -xSpeed;
                if(playerLeft.getLastMove() = 1){let ySpeed = MathExtention.abs(ySpeed);}// мяч летит туда, куда двигалась ракетка
                if(playerLeft.getLastMove() = 0){let ySpeed = -MathExtention.abs(ySpeed);}
            }
        }

        // Отбил правый игрок
        if (xSpeed > 0) {
            if (((xCord + radius) > playerRight.getX()) &
                (yCord > playerRight.getY()) & 
                (yCord < (playerRight.getY() + playerRight.getSizeY()))) {
                let xSpeed = -xSpeed;

                if(playerRight.getLastMove() = 1){let ySpeed = MathExtention.abs(ySpeed);}
                if(playerRight.getLastMove() = 0){let ySpeed = -MathExtention.abs(ySpeed);}
            }
        }

        // Счетчик правого игрока
        if ((xCord - radius) < 4) {
            do goToCenter();
            do playerRight.incrementScore();
            do updateScoreDisplay();
        } 

        // Счетчик левого игрока
        if ((xCord + radius) > 507) {
            do goToCenter();
            do playerLeft.incrementScore();
            do updateScoreDisplay();
        } 

        // Отрисовка мяча
        do Screen.setColor(true);
        do Screen.drawCircle(xCord, yCord, radius);

        return;
    }

    method int getLeftScore(){
        return playerLeft.getScore();
    }

    method int getRightScore(){
        return playerRight.getScore();
    }
}