class Player{
    field int x, y;
    field int sizeY, sizeX;
    field int step;
    field int score;
    field int side; //0 - left, 1 - right
    field int lastMove;// 0 - up, 1 - down 

    constructor Player new(int X, int Y, int Side, int ySize){
    let x = X;  
    let y = Y;

    let sizeY = ySize;
    let sizeX = 4;
    let step = 2;
    let score = 0;
    let side = Side;
    let lastMove = 0;
    
    return this;
    }


    method void moving(){
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, x + sizeX, y + sizeY);

        //управление ракетками
        if (side = 0) {
            if (Keyboard.keyPressed() = 87) { 
                do up();
                let lastMove = 0; }    
            if (Keyboard.keyPressed() = 83) { 
                do down();
                let lastMove = 1;}   
        }

        if (side = 1) {
            if (Keyboard.keyPressed() = 131) {
                 do up();
                let lastMove = 0; }      
            if (Keyboard.keyPressed() = 133) { 
                do down();
                let lastMove = 1; }    
        }
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, x + sizeX, y + sizeY);
        
        return;
    }
    
    method void up(){
        if(y - step > 30){
            let y = y - step;
        }
        return;
    }

    method void down(){
        if(y + step + sizeY < 247){
            let y = y + step;
        }
        return;
    }

    method int getX(){
        return x;
    }

    method int getY(){
        return y;
    }

    method int getSizeX(){
        return sizeX;
    }

    method int getSizeY(){
        return sizeY;
    }

    method void incrementScore(){
        let score = score + 1;
        return;
    }

    method int getScore(){
        return score;
    }
    
    method int getLastMove(){
        return lastMove;
    }
}